010 ' ========================================
020 ' PROGRAMA: IDENTIFICADOR DE TIPO USR
030 ' PASSA PARAMETRO PARA FUNCAO ASSEMBLY
040 ' QUE RETORNA O TIPO DA VARIAVEL
050 ' ========================================
060 GOSUB 2000:'CARREGA CODIGO ASSEMBLY
070 DEFUSR=&HE000  

200 ' ====== MENU DE TESTES ======
210 GOSUB 1000
220 LOCATE 1,4:? "TESTE DE TIPOS  "
230 LOCATE 1,5:? "================"
280 LOCATE 1,6:? "0-SAIR          "
290 LOCATE 1,7:? "1-TESTAR INTEIRO"
300 LOCATE 1,8:? "2-TESTAR STRING"
310 LOCATE 1,9:? "3-TESTAR SIMPLES PRECISAO"
320 LOCATE 1,10:? "4-TESTAR DUPLA PRECISAO"
330 LOCATE 1,17:? "ESCOLHA UMA OPCAO:"
340 LOCATE 27,21:OP$=INPUT$(1)
350 IF OP$="0" THEN RUN"AUTOEXEC.BAS"
360 ON VAL(OP$) GOSUB 400,500,600,700
370 GOTO 200

400 ' === TESTE INTEIRO ===
410 A%=100
420 T=USR(A%)
430 GOSUB 800
440 RETURN

500 ' === TESTE STRING ===
510 A$="MSX"
520 T$=USR(A$)
530 GOSUB 800
540 RETURN

600 ' === TESTE SIMPLES PRECISAO ===
610 A!=1.23
620 T!=USR(A!)
630 GOSUB 800
640 RETURN

700 ' === TESTE DUPLA PRECISAO ===
710 A#=4.5678
720 T#=USR(A#)
730 GOSUB 800
740 RETURN

800 ' === EXIBE TIPO ===
810 GOSUB 1000
820 T%=PEEK(&HE010)
830 LOCATE 1,4:? "TIPO RETORNADO: ";T%
840 IF T%=2 THEN LOCATE 1,5:? "INTEIRO (2 BYTES)"
850 IF T%=3 THEN LOCATE 1,6:? "STRING (3 BYTES)"
860 IF T%=4 THEN LOCATE 1,7:? "SIMPLES PRECISAO (4 BYTES)"
870 IF T%=8 THEN LOCATE 1,8:? "DUPLA PRECISAO (8 BYTES)"
880 LOCATE 27,21:OP$=INPUT$(1)
890 RETURN

1000 ' ========================================
1010 ' LIMPA A AREA UTIL DA TELA 
1020 ' ========================================
1030 FOR I = 4 TO 18
1040   LOCATE 1, I:? STRING$(27," ");
1050 NEXT I
1060 RETURN

2000 ' ======================================
2010 ' CARREGA O CODIGO ASSEMBLY      
2020 ' ======================================
2025 RESTORE 3000
2030 E=&HE000
2040 READ R$
2050 IF R$="*" THEN RETURN
2060 POKE E,VAL("&H"+R$)
2070 E=E+1
2080 GOTO 2040

3000 ' =======================================
3010 ' CODIGO ASSEMBLY PARA IDENTIFICAR TIPO
3020 ' =======================================
3030 DATA 3A,63,F6,32,10,E0,21,10,E0,C9,*