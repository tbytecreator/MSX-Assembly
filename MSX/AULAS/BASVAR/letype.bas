010 ' ========================================
020 ' PROGRAMA: IDENTIFICADOR DE TIPO USR
030 ' PASSA PARAMETRO PARA FUNCAO ASSEMBLY
040 ' QUE RETORNA O TIPO DA VARIAVEL
050 ' ========================================
060 CLEAR 200,&HE000
070 BLOAD"LETYPE.BIN"
080 DEFUSR=&HE000

200 ' ====== MENU DE TESTES ======
210 GOSUB 1000
220 LOCATE 1,4:? "TESTE DE TIPOS  "
230 LOCATE 1,5:? "================"
240 LOCATE 1,7:? "1-TESTAR INTEIRO"
250 LOCATE 1,8:? "2-TESTAR STRING "
260 LOCATE 1,9:? "3-TESTAR SINGLE "
270 LOCATE 1,10:? "4-TESTAR DOUBLE "
280 LOCATE 1,11:? "0-SAIR          "
290 LOCATE 1,13:? "ESCOLHA UMA OPCAO:"
300 LOCATE 27,21:OP$=INPUT$(1)
310 IF OP$="0" THEN RUN"AUTOEXEC.BAS"
320 ON VAL(OP$) GOSUB 400,500,600,700
330 GOTO 200

400 ' === TESTE INTEIRO ===
410 A%=1234
420 T=USR(A%)
430 GOSUB 800
440 RETURN

500 ' === TESTE STRING ===
510 A$="TESTE MSX"
520 T=USR(A$)
530 GOSUB 800
540 RETURN

600 ' === TESTE SINGLE ===
610 A!=123.45
620 T=USR(A!)
630 GOSUB 800
640 RETURN

700 ' === TESTE DOUBLE ===
710 A#=123.456789#
720 T=USR(A#)
730 GOSUB 800
740 RETURN

800 ' === EXIBE TIPO ===
810 GOSUB 1000
820 T=PEEK(&HE010)
830 LOCATE 1,4:? "TIPO RETORNADO: ";T
840 IF T=2 THEN LOCATE 1,5:? "INTEIRO (2 BYTES)"
850 IF T=3 THEN LOCATE 1,5:? "STRING"
860 IF T=4 THEN LOCATE 1,5:? "SINGLE (4 BYTES)"
870 IF T=8 THEN LOCATE 1,5:? "DOUBLE (8 BYTES)"
880 LOCATE 27,21:OP$=INPUT$(1)
890 RETURN

1000 ' ------------------------------
1010 ' LIMPA A AREA UTIL DA TELA 
1020 ' ------------------------------
1030 FOR I = 4 TO 18
1040   LOCATE 1, I:? STRING$(27," ");
1050 NEXT I
1060 RETURN